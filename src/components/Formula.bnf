{ ws=explicit }
Expression ::= Primitive | Reference | FunctionCall | Identifier

FunctionCall ::= Identifier FunctionArguments {ws=implicit}
FunctionArguments ::= "(" ( Expression (WS* ";" WS* Expression)* )? ")" { pin=1, ws=implicit }

Reference ::= GlobalReference | LocalReference
GlobalReference ::= Sheet "!" LocalReference
LocalReference ::= CellAddress | Range | Vector

Sheet ::= Identifier

Range ::= CellAddress ":" CellAddress
Vector ::= ColumnRange | RowRange
ColumnRange ::= ColumnAddress ":" ColumnAddress
RowRange ::= RowAddress ":" RowAddress

CellAddress ::= ColumnAddress RowAddress
RowAddress ::= "$" Row | Row
Row ::= NUMBER
ColumnAddress ::= "$" Column | Column
Column ::= LETTER LETTER | LETTER

Primitive ::= Number | Boolean | String

Number ::= "-"? ("0" | [1-9] [0-9]*) ("." [0-9]+)? (("e" | "E") ( "-" | "+" )? ("0" | [1-9] [0-9]*))?
Integer ::= "0" | [1-9] [0-9]*
Boolean ::= "TRUE" | "FALSE"

Identifier ::= IDENTIFIER_FIRST_CHAR IDENTIFIER_CHAR*

IDENTIFIER_FIRST_CHAR ::= LETTER | "_"
IDENTIFIER_CHAR ::= LETTER | NUMBER | "_"

CHARACTER ::= LETTER | DIGIT
LETTER ::= "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" | "j" | "k" | "l" | "m" | "n" | "o" | "p" | "q" | "r" | "s" | "t" | "u" | "v" | "w" | "x" | "y" | "z" | "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" | "J" | "K" | "L" | "M" | "N" | "O" | "P" | "Q" | "R" | "S" | "T" | "U" | "V" | "W" | "X" | "Y" | "Z"

NUMBER ::= DIGIT NUMBER | DIGIT
DIGIT ::= [0-9]

String          ::= '"' CHAR* '"'
CHAR            ::= !"$" UNESCAPED
UNESCAPED       ::= [#x20-#x21] | [#x23-#x5B] | [#x5D-#xFFFF]
WS              ::= [#x20#x09#x0A#x0D]+
